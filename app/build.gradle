plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.10'
    id 'dagger.hilt.android.plugin'
    id "org.jetbrains.kotlin.kapt"
    id 'com.google.protobuf'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.egon12.developerhelper"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.12.0' }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.35.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {

    // Standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.21"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'

    // View Pager
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // OKHTTP
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1")
    implementation("com.squareup.okhttp3:okhttp:4.9.0")
    //implementation("com.github.TellH:RecyclerTreeView:1.2.0")

    // GRPC
    implementation 'io.grpc:grpc-okhttp:1.35.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-protobuf:1.35.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.35.0' // CURRENT_GRPC_VERSION
    //implementation 'com.google.protobuf:protobuf-javalite:3.12.0'
    //implementation 'com.google.protobuf:protobuf-java:3.12.0'
    compileOnly("org.glassfish:javax.annotation:10.0-b28")

    // JDBC Driver
    implementation(group: 'mysql', name: 'mysql-connector-java', version: '5.1.6')
    implementation(group: 'org.postgresql', name: 'postgresql', version: '9.4.1212')

    // Room Database
    implementation "androidx.room:room-runtime:2.2.6"
    implementation "androidx.room:room-ktx:2.2.6"
    kapt "androidx.room:room-compiler:2.2.6"

    // Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.30.1-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.30.1-alpha"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'

    // Dagger Hil - Testing
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.28-alpha'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.30.1-alpha'

    // Standard Test Library
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'com.jraska.livedata:testing-ktx:1.1.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"

    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "io.mockk:mockk-android:1.10.0"

    // Fragment Test
    //debugImplementation "androidx.fragment:fragment-testing:1.2.5"
}